model lasertag

use Mars

agent Green on Battleground {
//	*********************** general attributes ***********************
	external observe var xSpawn : real
	external observe var ySpawn : real
	external observe var memberID : integer
	observe var color : string = "green"
	observe var energy : integer = 100
	observe var actionPoints : integer = 10
	observe var stage : integer = 0
	observe var points : integer = 0
//	*********************** movement attributes ***********************
	// values: standing, kneeling, lying
	observe var currStance : string = "standing"
	observe var movementDelay : integer = 0
	observe var hasMoved : bool = false
//	*********************** exploration attributes ***********************
	observe var visualRange : real = 10
	observe var visibilityRange : real = 10
	var barriers = new List<Tuple<real, real>>
	var hills = new List<Tuple<real, real>>
	var ditches = new List<Tuple<real, real>>
	var enemiesFromTeam = new List<Tuple<real, real>>
//	*********************** tagging attributes ***********************
	observe var magazineCount : integer = 5
	observe var wasTagged : bool = false
	observe var wasTaggedCounter : integer = 0
	observe var tagged : bool = false
	observe var taggedCounter : integer = 0
//	*********************** pathfinding attributes ***********************
	var g : integer = 1000
	var rhs : integer = 1000
	var km : real = 0.0
	// tuple values: (<x>, <y>, <g>, <rhs>, <cost> <key1>, <key2>)
	var sStart : Tuple<real, real, integer, integer, integer, real, real>
	var sGoal : Tuple<real, real, integer, integer, integer, real, real>
	var sLast : Tuple<real, real, integer, integer, integer, real, real>
	var routeList = new List<Tuple<real, real, integer, integer, integer, real, real>>()
	var expandQueue = new List<Tuple<real, real, integer, integer, integer, real, real>>()
	var pathCalculated : bool = false
	
	initialize {
		var x : real = xSpawn
		var y : real = ySpawn
		pos at #(x, y)
		sGoal = #(x, y, g, rhs, 0, 0.0, 0.0)
	}
	
	tick {
		// TODO: your AI here

		refillPoints()
	}

// **************************************************************************************** 
// ****************************** EXPLORATION AND VISION **********************************
// ****************************************************************************************
	
	// USER METHOD: exploration of environment
	// o: "barriers", "hills", "ditches"
	def exploreEnv(o : string) {
		if (actionPoints < 1) { return }
		switch (o) {
			case "barriers" : { exploreBarriers() }
			case "hills" : { exploreHills() }
			case "ditches" : { exploreDitches() }
		}
		actionPoints--
	}
	
	// generates a list of coordinates within visualRange that contain a wall
	def exploreBarriers() {
		var currBarriers = explore Barrier where [b => return ((distance b) <= visualRange and hasBeeline(xcor, ycor, b.GetX, b.GetY))]
		each (b in currBarriers) {
			var inList : bool = false
			each (e in barriers) {
				if ((e.Item1 === b.GetX) and (e.Item2 === b.GetY)) { inList = true }
			}			
			if (not inList) { barriers.Add(#(b.GetX, b.GetY)) }
		}
	}
	
	def exploreHills() {
		var currHills = explore Hill where [h => return ((distance h) <= visualRange and hasBeeline(xcor, ycor, h.GetX, h.GetY))]
		each (h in currHills) {
			var inList : bool = false
			each (e in hills) {
				if ((e.Item1 === h.GetX) and (e.Item2 === h.GetY)) { inList = true }
			}
			if (not inList) { hills.Add(#(h.GetX, h.GetY)) }
		}
	}
	
	def exploreDitches() {
		var currDitches = explore Ditch where [d => return ((distance d) <= visualRange and hasBeeline(xcor, ycor, d.GetX, d.GetY))]
		each (d in currDitches) {
			var inList : bool = false
			each (e in ditches) {
				if ((e.Item1 === d.GetX) and (e.Item2 === d.GetY)) { inList = true }
			}
			if (not inList) { ditches.Add(#(d.GetX, d.GetY)) }
		}
	}

	// USER METHOD: generates an array of team members
	def exploreTeam() : Green[] {
		var team = explore Green where [x => return (actionPoints > 0)]
		if (actionPoints > 0) { actionPoints-- }
		return team
	}
	
	// USER METHOD: generates arrays of enemies who can be seen
	// three conditions for being able to see an enemy:
		// 1) enemy coordinates are within visualRange
		// 2) enemy visibility makes enemy visible from my location
		// 3) no walls blocking linear vision between enemy and me
	def exploreEnemies() : Tuple<Red[], Blue[], Yellow[]> {
		var enemies = #(explore Red where [x => return (actionPoints > 0) and (((distance x) <= visualRange)) and (((distance x) <= x.GetVisibility) or (battleground.GetIntegerValue(xcor, ycor) === 2) or (battleground.GetIntegerValue(xcor, ycor) === 3)) and hasBeeline(xcor, ycor, x.GetX, x.GetY)],
						explore Blue where [x => return (actionPoints > 0) and (((distance x) <= visualRange)) and (((distance x) <= x.GetVisibility) or (battleground.GetIntegerValue(xcor, ycor) === 2) or (battleground.GetIntegerValue(xcor, ycor) === 3)) and hasBeeline(xcor, ycor, x.GetX, x.GetY)],
						explore Yellow where [x => return (actionPoints > 0) and (((distance x) <= visualRange)) and (((distance x) <= x.GetVisibility) or (battleground.GetIntegerValue(xcor, ycor) === 2) or (battleground.GetIntegerValue(xcor, ycor) === 3)) and hasBeeline(xcor, ycor, x.GetX, x.GetY)]
						)
		var teamArray = explore Green
		for (var i = 0; i < length(teamArray); i++) {
			var a = teamArray[i]
			a.ForwardEnemies(enemies)
		}
		if (actionPoints > 0) { actionPoints-- }
		return enemies
	}
	
	passive ForwardEnemies(enemies : Tuple<Red[], Blue[], Yellow[]>) {
		for (var i = 0; i < length(enemies.Item1); i++) {
			var red = enemies.Item1
			var a = red[i]
			enemiesFromTeam.Add(#(a.GetX, a.GetY))
		}
		for (var i = 0; i < length(enemies.Item2); i++) {
			var blue = enemies.Item2
			var a = blue[i]
			enemiesFromTeam.Add(#(a.GetX, a.GetY))
		}
		for (var i = 0; i < length(enemies.Item3); i++) {
			var yellow = enemies.Item3
			var a = yellow[i]
			enemiesFromTeam.Add(#(a.GetX, a.GetY))
		}
	}
	
	// USER METHOD: gets barriers, hills, or ditches list from team member a, based on value of o
	def getInfoFromTeam(o : string, a : Green) : List<Tuple<real, real>> {
		switch (o) {
			case "barriers" : { return a.GetBarriers }
			case "hills" : { return a.GetHills }
			case "ditches" : {return a.GetDitches }
		}
	}
	
	// USER METHOD: interface for checking if the line of sight between agent and (x, y) is blocked
	def hasBeeline(x : real, y : real) : bool {
		return hasBeeline(xcor, ycor, x, y)
	}
	
	// implementation of Bresenham's Line Algorithm for obtaining a list of grid cells covered by a straight line between two points on the grid
	// http://tech-algorithm.com/articles/drawing-line-using-bresenham-algorithm/ for more information
	def hasBeeline(x1 : real, y1 : real, x2 : real, y2 : real) : bool {
	    var hasBeeline : bool = true
	    var x : integer = x1 as integer
	    var y : integer = y1 as integer
	    var newX2 : integer = x2 as integer
	    var newY2 : integer = y2 as integer
	    var w : integer = newX2 - x
	    var h : integer = newY2 - y
	    var dx1 : integer = 0
	    var dy1 : integer = 0
	    var dx2 : integer = 0
	    var dy2 : integer = 0
	    if (w < 0) { dx1 = -1 }
	    else if (w > 0) { dx1 = 1 }
	    if (h < 0) { dy1 = -1 }
	    else if (h > 0) { dy1 = 1 }
	    if (w < 0) { dx2 = -1 }
		else if (w > 0) { dx2 = 1 }
	    var longest : integer = Math.Abs(w)
	    var shortest : integer = Math.Abs(h)
	    if (not(longest > shortest)) {
	        longest = Math.Abs(h)
	        shortest = Math.Abs(w)
	        if (h < 0) { dy2 = -1 }
	    	else if (h > 0) { dy2 = 1 }
	        dx2 = 0          
	    }
	    var numerator : integer = longest / 2
	    for (var i=0; i < longest; i++) {
	        if ((battleground.GetIntegerValue(x as real, y as real) === 1) or (battleground.GetIntegerValue(x as real, y as real) === 2)) {
				hasBeeline = false
				return hasBeeline
			}
	        numerator = numerator + shortest
	        if (not (numerator < longest)) {
	            numerator = numerator - longest
	            x = x + dx1
	            y = y + dy1
	        } else {
	            x = x + dx2
	            y = y + dy2
	        }
	    }
	    return hasBeeline
	}
	
	// USER METHOD: gets distance between agent and #(x, y), if possible
	// return: distance OR -1.0 if distance cannot be determined
	def getDistance(x : real, y : real) : real {
		var isInList : bool = false
		for (var i = 0; i < enemiesFromTeam.Size(); i++) {
			if ((enemiesFromTeam.Get(i).Item1 === x) and (enemiesFromTeam.Get(i).Item2 === y)) { isInList = true }
		}
		if (hasBeeline(x, y) or hills.Exists([h => return h === #(x, y)]) or ditches.Exists([d => return d === #(x, y)]) or barriers.Exists([b => return b === #(x, y)]) or isInList) {
			return Math.Max(Math.Abs(xcor - x), Math.Abs(ycor - y))
		}
		return -1.0
	}
	
	// USER METHOD: gets distance between agent and a, if possible
	// return distance OR -1.0 if distance cannot be determined
	def getDistance(a : Green) : real {
		if (hasBeeline(a.GetX, a.GetY)) { return Math.Max(Math.Abs(xcor - a.GetX), Math.Abs(ycor - a.GetY)) }
		return -1.0
	}
	
	// USER METHOD: gets distance between agent and a, if possible
	// return distance OR -1.0 if distance cannot be determined
	def getDistance(a : Red) : real {
		if (hasBeeline(a.GetX, a.GetY)) { return Math.Max(Math.Abs(xcor - a.GetX), Math.Abs(ycor - a.GetY)) }
		return -1.0
	}
	
	// USER METHOD: gets distance between agent and a, if possible
	// return distance OR -1.0 if distance cannot be determined
	def getDistance(a : Blue) : real {
		if (hasBeeline(a.GetX, a.GetY)) { return Math.Max(Math.Abs(xcor - a.GetX), Math.Abs(ycor - a.GetY)) }
		return -1.0
	}
	
	// USER METHOD: gets distance between agent and a, if possible
	// return distance OR -1.0 if distance cannot be determined
	def getDistance(a : Yellow) : real {
		if (hasBeeline(a.GetX, a.GetY)) { return Math.Max(Math.Abs(xcor - a.GetX), Math.Abs(ycor - a.GetY)) }
		return -1.0
	}
	
// **************************************************************************************** 
// *********************************** MOVEMENT BEGIN *************************************
// ****************************************************************************************
	
	// USER METHOD: main method for pathfinding and movement algorithm
	// implementation of modified D* Lite Algorithm for Agent pathfinding, movement, and path readjustment in case of unforeseen obstacles
	// xGoal: x-coordinate of grid cell Guest wants to move to
	// yGoal: y-coordinate of grid cell Guest wants to move to
	// return: boolean states if step was successfully taken
	def goTo(inXGoal : real, inYGoal : real) : bool {
		var xGoal : real = Math.Floor(inXGoal)
		var yGoal : real = Math.Floor(inYGoal)
		if (battleground.GetIntegerValue(xGoal, yGoal) === 1) {
			hasMoved = true
			return false
		}
		if ((movementDelay > 0) or hasMoved or ((xGoal === xcor) and (yGoal === ycor))) { return false }
		if (xGoal !== sGoal.Item1 or yGoal !== sGoal.Item2) { pathCalculated = false }
		if (not pathCalculated) { initPathfinding(xGoal, yGoal) }
		if (not pathCalculated) { return false }
		sStart = findNextCell()
		var neighsInRouteList = findNeighsInRouteList()
		var neighsWithChangedCost = scanCostChanges(neighsInRouteList)
		if (neighsWithChangedCost.Size() !== 0) {
			// calculation of completely new path
			var newExpandQueue = new List<Tuple<real, real, integer, integer, integer, real, real>>
			expandQueue = newExpandQueue
			var newRouteList = new List<Tuple<real, real, integer, integer, integer, real, real>>
			routeList = newRouteList
			var keyInit = Math.Max(Math.Abs(xcor - xGoal), Math.Abs(ycor - yGoal))
			sGoal = #(xGoal, yGoal, g, 0, battleground.GetIntegerValue(xGoal, yGoal), keyInit, 0.0)
			expandQueue.Add(sGoal)
			km = 0.0
			sStart = #(xcor, ycor, g, rhs, battleground.GetIntegerValue(xcor, ycor), 1000.0, 1000.0)
			sLast = sStart
			computeShortestPath(sStart, km)
			sStart = findNextCell()
		}
		if (battleground.GetIntegerValue(sStart.Item1, sStart.Item2) === 2) {
			var hill = explore Hill where [h => return (h.GetX === sStart.Item1) and (h.GetY === sStart.Item2)]
			if (not hill[0].IsFree) {
				pathCalculated = false
				return false
			}
		} else if (battleground.GetIntegerValue(sStart.Item1, sStart.Item2) === 3) {
			var ditch = explore Ditch where [d => return (d.GetX === sStart.Item1) and (d.GetY === sStart.Item2)]
			if (not ditch[0].IsFree) {
				pathCalculated = false
				return false
			}
		}
		moveMe(sStart.Item1, sStart.Item2)
		if ((xcor === sGoal.Item1) and (ycor === sGoal.Item2)) { pathCalculated = false	}
		return true
	}
	
	// computes initial route from (xcor, ycor) to (<xGoal>, <yGoal>)
	// xGoal: x-coordinate of grid cell Guest wants to move to
	// yGoal: y-coordinate of grid cell Guest wants to move to
	def initPathfinding(xGoal : real, yGoal : real) {
		var costSStart = battleground.GetIntegerValue(xcor, ycor)
		sStart = #(xcor, ycor, g, rhs, costSStart, 1000.0, 1000.0)
		sLast = sStart
		var keyInit = Math.Max(Math.Abs(xcor - xGoal), Math.Abs(ycor - yGoal))
		var costSGoal = battleground.GetIntegerValue(xGoal, yGoal)
		sGoal = #(xGoal, yGoal, g, 0, costSGoal, keyInit, 0.0)
		expandQueue = new List<Tuple<real, real, integer, integer, integer, real, real>>
		routeList = new List<Tuple<real, real, integer, integer, integer, real, real>>
		expandQueue.Add(sGoal)
		pathCalculated = true
		computeShortestPath(sStart, km)
	}
	
	// computes shortest path from current position to goal
	// sStart: tuple representing grid cell from which Guest starts moving towards goal
	// km: key modifier value
	def computeShortestPath(sStart : Tuple<real, real, integer, integer, integer, real, real>, km : real) {
		var u : Tuple<real, real, integer, integer, integer, real, real> = getTopKey()
		while ((u.Item6 !== 0.0) and ((u.Item6 < calcKey1(sStart, km)) or (sStart.Item4 > sStart.Item3))) {
			var kOld1 = u.Item6
			var kNew1 = calcKey1(u, km)
			var kNew2 = calcKey2(u)
			if (kOld1 < kNew1) {
				var v = #(u.Item1, u.Item2, u.Item3, u.Item4, u.Item5, kNew1, kNew2)
				updateQueue(u, v)
			} else if (u.Item3 > u.Item4) {
				var uNew = #(u.Item1, u.Item2, u.Item4, u.Item4, u.Item5, u.Item6, u.Item7)
				routeList.Add(uNew)
				var NeighList = Neigh(uNew)
				removeFromQueue(u)
				each (var s in NeighList) {
					if ((s.Item1 !== sGoal.Item1) or (s.Item2 !== sGoal.Item2)) {
						var newRHS = RHS(s, uNew)
						var sNew = #(s.Item1, s.Item2, s.Item3, newRHS, s.Item5, s.Item6, s.Item7)
						updateVertex(sNew, km)
					}
				}
			} else {
				var gOld = u.Item3
				var uNew = #(u.Item1, u.Item2, g, u.Item4, u.Item5, u.Item6, u.Item7)
				updateQueue(u, uNew)
				var neighList = Neigh(uNew)
				neighList.Add(uNew)
				each (var s in neighList) {
					var updated : bool = false
					if (s.Item4 === (u.Item5 + gOld)) {
						if ((s.Item1 !== sGoal.Item1) or (s.Item2 !== sGoal.Item2)) {
							var neighListTemp = Neigh(s)
							var newRHS = getMinRHS(s, neighListTemp)
							var newS = #(s.Item1, s.Item2, s.Item3, newRHS, s.Item5, s.Item6, s.Item7)
							updateVertex(newS, km)
							updated = true
						}
					}
					if (not updated) { updateVertex(s, km) }
				}
			}
			u = getTopKey()
			if ((u.Item1 === -1.0) and (u.Item2 === -1.0)) { return }
		}
	}
	
	// determines which cell an Guest should move to next
	// return: tuple representing grid cell that Guest needs to move to
	def findNextCell() : Tuple<real, real, integer, integer, integer, real, real> {
		var nextCellCandidates = findNeighsInRouteList()
		var minNeigh = nextCellCandidates.Get(0)
		each (var neigh in nextCellCandidates) {
			if ((cost(neigh, sStart) + neigh.Item3) < (cost(minNeigh, sStart) + minNeigh.Item3)) {
				minNeigh = neigh
			} else if ((cost(neigh, sStart) + neigh.Item3) === (cost(minNeigh, sStart) + minNeigh.Item3)) {
				if ((xcor === neigh.Item1) or (ycor === neigh.Item2)) {
					minNeigh = neigh
				}
			}
		}
		return minNeigh
	}
	
	// creates a list of tuples representing grid cells that are neighbors of sStart and that are in routeList
	// return: list of tuples
	def findNeighsInRouteList() : List<Tuple<real, real, integer, integer, integer, real, real>> {
		var nextCellCandidates = new List<Tuple<real, real, integer, integer, integer, real, real>>()
		each (var s in routeList) {
			if (heur(sStart, s) === 1.0) { nextCellCandidates.Add(s) }
		}
		return nextCellCandidates
	}
  
	// determines for all elements of List if their cost is not equal to 1.
	// List: neighbors of sStart
	// return: list with neighbors of sStart that are in routeList and whose cost is not equal to 1
	def scanCostChanges(neighsinRouteList : List<Tuple<real, real, integer, integer, integer, real, real>>) : List<Tuple<real, real, integer, integer, integer, real, real>> {
		var currCostNeigh : integer
		var neighsWithChangedCost = new List<Tuple<real, real, integer, integer, integer, real, real>>()
		each(currNeigh in neighsinRouteList){
			currCostNeigh = cost(currNeigh, sStart)
			if (currCostNeigh !== currNeigh.Item5) { neighsWithChangedCost.Add(currNeigh) }
		}
		return neighsWithChangedCost
	}
	
	// obtains the next cell that should be examined during pathfinding
	// return: tuple with most favorable key1 and key2
	def getTopKey() : Tuple<real, real, integer, integer, integer, real, real> {
    	if (expandQueue.Size() === 0) {
    		pathCalculated = false
    		return #(-1.0, -1.0, 0, 0, 0, 0.0, 0.0)
    	}
    	var minKey1 : real = expandQueue.Get(0).Item6
    	var minKey2 : real = expandQueue.Get(0).Item7
    	each (currElem in expandQueue) {
      		if (currElem.Item6 < minKey1) {
				minKey1 = currElem.Item6
				minKey2 = currElem.Item7
			} else if ((currElem.Item6 === minKey1) and (currElem.Item7 < minKey2)) { minKey2 = currElem.Item7 }
		}
		var minKeyTuple : Tuple<real, real, integer, integer, integer, real, real>
		for (var i = 0; i < expandQueue.Size(); i++) {
			if ((expandQueue.Get(i).Item6 === minKey1) and (expandQueue.Get(i).Item7 === minKey2)) {
				minKeyTuple = expandQueue.Get(i)
				i = expandQueue.Size()
			}
		}
		return minKeyTuple
	}
	
	// calculates the first part of the key of a grid cell (using heuristic)
	// s: tuple for which key1 is to be calculated
	// km: key modifier value
	// return: key1 value of s
	def calcKey1(s : Tuple<real, real, integer, integer, integer, real, real>, km : real) : real {
		return Math.Min(s.Item3, s.Item4) + heur(sStart, s) + km
	}
	
	// heuristic: takes the larger value of the x-range and y-range between two grid cells
	// s1: tuple representing the first grid cell to be used in heuristic calculation
	// s2: tuple representing the second grid cell to be used in heuristic calculation
	// return: result of heuristic calculation
	def heur(s1 : Tuple<real, real, integer, integer, integer, real, real>, s2 : Tuple<real, real, integer, integer, integer, real, real>) : real {
		return Math.Max(Math.Abs(s1.Item1 - s2.Item1), Math.Abs(s1.Item2 - s2.Item2))
	}
	
	// calculates the second part of the key of a grid cell
	// s: tuple for which key2 is to be calculated
	// return: key2 value of s
	def calcKey2(s : Tuple<real, real, integer, integer, integer, real, real>) : real {
		var key2 : real = Math.Min(s.Item3, s.Item4)
		return key2
	}
	
	// updates expandQueue by calling removeFromQueue method and adding a new tuple
	// u: tuple to be removed from expandQueue
	// newU: tuple to be added to expandQueue
	def updateQueue(u : Tuple<real, real, integer, integer, integer, real, real>, newU : Tuple<real, real, integer, integer, integer, real, real>) {
		removeFromQueue(u)
		expandQueue.Add(newU)
	}
	
	// updates routeList by creating a copy and adding s and all entries from old routeList unequal to s to it
	// s: new tuple to be added to routeList to replace old version of s
	def updateRouteList(s : Tuple<real, real, integer, integer, integer, real, real>) {
		var newRouteList = new List<Tuple<real, real, integer, integer, integer, real, real>>()
		each (var entry in routeList) {
			if ((entry.Item1 !== s.Item1) or (entry.Item2 !== s.Item2)) { newRouteList.Add(entry) }
			if ((entry.Item1 === s.Item1) and (entry.Item2 === s.Item2)) { newRouteList.Add(s) }
		}
		routeList = newRouteList
	}
	
	// removes a tuple from expandQueue
	// u: tuple to be removed from expandQueue
	def removeFromQueue(u : Tuple<real, real, integer, integer, integer, real, real>) {
		var newExpandQueue = new List<Tuple<real, real, integer, integer, integer, real, real>>()
		each (var s in expandQueue) {
			if ((s.Item1 !== u.Item1) or ((s.Item2 !== u.Item2))) { newExpandQueue.Add(s) }
		}
		expandQueue = newExpandQueue
	}
	
	// finds all grid cells neighboring s (and whether they have representative tuples in routeList or expandQueue)
	// s: tuple representing grid cell of which neighbors are to be found
	// return: list containing tuples representing neighbors of s
	def Neigh(s : Tuple<real, real, integer, integer, integer, real, real>) : List<Tuple<real, real, integer, integer, integer, real, real>> {
		var NeighList = new List<Tuple<real, real, integer, integer, integer, real, real>>()
		var x : real
		var y : real
		for (x = s.Item1 - 1.0; x <= s.Item1 + 1.0; x++) {
			for (y = s.Item2 - 1.0; y <= s.Item2 + 1.0; y++) {
				if ((x !== s.Item1) or (y !== s.Item2)) {
					var found : bool = false
					each (r in routeList) {
						if ((r.Item1 === x) and (r.Item2 === y)) {
							NeighList.Add(r)
							found = true
						}
					}
					each (e in expandQueue) {
						if ((e.Item1 === x) and (e.Item2 === y)) {
							NeighList.Add(e)
							found = true
						}
					}
					if (not found) {
						var neigh = #(x, y, g, rhs, 0, 1000.0, 1000.0)
						var costNeigh = cost(neigh, s)
						if (costNeigh !== 1000) {
							var newNeigh = #(x, y, g, rhs, costNeigh, 1000.0, 1000.0)
							NeighList.Add(newNeigh)
						}
					}
				}
			}
		}
		return NeighList
	}
	
	// calculates the rhs value of tuple based on another tuple
	// s: tuple representing grid cell neighboring u
	// u: tuple representing grid cell currently being examined
	// return: updated rhs value
	def RHS(s : Tuple<real, real, integer, integer, integer, real, real>, u : Tuple<real, real, integer, integer, integer, real, real>) : integer {
		if ((s.Item1 === sStart.Item1) and (s.Item2 === sStart.Item2)) { return 0 }
		else {
			var newRHS : integer = Math.Min(s.Item4, (cost(u, s) + u.Item3))
			return newRHS
		}
	}
	
	// updates tuple u by adding it to or removing it from expandQueue or changing its information
	// u: tuple to be updated
	// km: key modifier value
	def updateVertex(u : Tuple<real, real, integer, integer, integer, real, real>, km : real) {
		var isInQueue : bool = inQueue(u)
		var newKey1 = calcKey1(u, km)
		var newKey2 = calcKey2(u)
		if ((u.Item3 !== u.Item4) and isInQueue) {
			var uNew = #(u.Item1, u.Item2, u.Item3, u.Item4, u.Item5, newKey1, newKey2)
			updateQueue(u, uNew)
		} else if ((u.Item3 !== u.Item4) and (not isInQueue)) {
			var uNew = #(u.Item1, u.Item2, u.Item3, u.Item4, u.Item5, newKey1, newKey2)
			expandQueue.Add(uNew)
		} else if ((u.Item3 === u.Item4) and (isInQueue)) {
			routeList.Add(u)
			removeFromQueue(u)
		}
	}
	
	// checks if a tuple is in expandQueue
	// u: tuple to be checked
	// return: boolean
	def inQueue(u : Tuple<real, real, integer, integer, integer, real, real>) : bool {
		each (element in expandQueue) {
			if ((element.Item1 === u.Item1) and (element.Item2 === u.Item2)) { return true }
		}
		return false
	}
	
	// checks if a tuple is in routeList
	// u: tuple to be checked
	// return: boolean
	def inRouteList(u : Tuple<real, real, integer, integer, integer, real, real>) : bool {
		each (element in routeList) {
			if ((element.Item1 === u.Item1) and (element.Item2 === u.Item2)) { return true }
		}
		return false
	}
	
	// computes cost of moving from s to u and checks numeric value of s on grid layer
	// s: tuple representing the first grid cell to be used in cost calculation
	// u: tuple representing the second grid cell to be used in cost calculation
	// return: cost value on s
	def cost(s : Tuple<real, real, integer, integer, integer, real, real>, u : Tuple<real, real, integer, integer, integer, real, real>) : integer {
		var dist = heur(s, u)
		if (dist === 1.0) {
			if (battleground.GetIntegerValue(s.Item1, s.Item2) === 1) {	return 1000	}
			else if ((battleground.GetIntegerValue(s.Item1, s.Item2) === 2) or (battleground.GetIntegerValue(s.Item1, s.Item2) === 3)) { return 100	}
			else { return 1	}
		} else if (dist < 1.0) { return 0 }
		else { println("s and u are not neighbors!") }
		return 1
	}
	
	// obtains minimum rhs-value of all neighbors of s
	// s: tuple representing grid cell whose neighbors minimum rhs value is to be determined
	// neighListTemp: list of tuples representing grid cells that neighbor s
	// return: smallest rhs values among neighbors of s
	def getMinRHS(s : Tuple<real, real, integer, integer, integer, real, real>, neighListTemp: List<Tuple<real, real, integer, integer, integer, real, real>>) : integer {
		var minRHS = neighListTemp.Get(0).Item3 + cost(neighListTemp.Get(0), s)
		each (var neigh in neighListTemp) {
			var compRHS : integer = neigh.Item3 + cost(neigh, s)
			if (compRHS < minRHS) { minRHS = compRHS }
		}
		return minRHS
	}
	
	// prints a list of tuples
	// list: list of tuples to be printed
	def printQueue(list : List<Tuple<real, real, integer, integer, integer, real, real>>) {
		each (element in list) { println(element) }
	}

	// moves agent, if possible, onto the grid cell with coordinates (x, y)
	def moveMe(x : real, y : real) {
		// moving away from a hill or ditch
		switch (battleground.GetIntegerValue(xcor, ycor)) {
			// hill
			case 2 : {
				var hill = nearest Hill
				hill.Free
				visibilityRange = visibilityRange - 3
				visualRange = visualRange - 3
			}
			// ditch
			case 3 : {
				var ditch = nearest Ditch
				ditch.Free
				visibilityRange = visibilityRange + 3
				visualRange = visualRange + 3
			}
			default : { }
		}
		// moving onto/into a hill or ditch		
		switch (battleground.GetIntegerValue(x, y)) {
			// hill
			case 2 : {
				var hill = explore Hill where [h => return (h.GetX === x) and (h.GetY === y)]
				hill[0].Occupy
				visibilityRange = visibilityRange + 3
				visualRange = visualRange + 3
			}
			// ditch
			case 3 : {
				var ditch = explore Ditch where [d => return (d.GetX === x) and (d.GetY === y)]
				ditch[0].Occupy
				visibilityRange = visibilityRange - 3
				visualRange = visualRange - 3
			}
		}
		move me to #(x, y)
		switch (currStance) {
			case "standing" : {	movementDelay = 0 }
			case "kneeling" : { movementDelay = 2 }
			case "lying" : { movementDelay = 3 }
		}
		hasMoved = true
	}

// **************************************************************************************** 
// ********************************* STANCE AND TAGGING ***********************************
// ****************************************************************************************

	// USER METHOD: changes currStance to <newStance>
	def changeStance(newStance : string) {
		if (actionPoints < 2) {	return }
		if (newStance === currStance) {	return }
		switch (currStance) {
			case "standing": {
				switch (newStance) {
					case "kneeling" : {
						visualRange = visualRange - 2
						visibilityRange = visibilityRange - 2
						movementDelay = 2
					}
					case "lying" : {
						visualRange = visualRange - 5
						visibilityRange = visibilityRange - 5
						movementDelay = 3
					}
				}
			}
			case "kneeling" : {
				switch (newStance) {
					case "standing" : {
						visualRange = visualRange + 2
						visibilityRange = visibilityRange + 2
						movementDelay = 0
					}
					case "lying" : {
						visualRange = visualRange - 3
						visibilityRange = visibilityRange - 3
						movementDelay = 3
					}
				}
			}
			case "lying" : {
				switch (newStance) {
					case "standing" : {
						visualRange = visualRange + 5
						visibilityRange = visibilityRange + 5
						movementDelay = 0
					}
					case "kneeling" : {
						visualRange = visualRange + 3
						visibilityRange = visibilityRange + 3
						movementDelay = 2
					}
				}
			}
		}
		currStance = newStance
		actionPoints = actionPoints - 2	
	}

	// USER METHOD: tags a red enemy
	def tag(red : Red) {
		if (magazineCount < 1) { reload() }
		if (actionPoints < 5) { return }
		var tagReturn : bool = false
		var enemyStanceVal : integer = 2
		var energyZero : bool = false
		if (battleground.GetIntegerValue(red.GetX, red.GetY) === 2) { enemyStanceVal = 2 }
		else if (battleground.GetIntegerValue(red.GetX, red.GetY) === 3) { enemyStanceVal = 0 }
		else {
			switch (red.GetStance) {
				case "kneeling" : {	enemyStanceVal = 1 }
				case "lying" : { enemyStanceVal = 0	}
			}
		}
		switch (currStance) {
			case "standing" : {
				if (random(10) + 1 + enemyStanceVal > 8) {
					energyZero = red.Tagged
					tagReturn = true
				}
			}
			case "kneeling" : {
				if (random(10) + 1 + enemyStanceVal > 6) {
					energyZero = red.Tagged
					tagReturn = true
				}
			}
			case "lying" : {
				if (random(10) + 1 + enemyStanceVal > 4) {
					energyZero = red.Tagged
					tagReturn = true
				}
			}
		}
		if (tagReturn) {
			tagged = true
			taggedCounter = 2
			if (red.GetEnergy >= 0) { points = points + 10 }
			if (energyZero) { points = points + 10 }
		}
		magazineCount--
		actionPoints = actionPoints - 5
	}

	// USER METHOD: tags a blue enemy
	def tag(blue : Blue) {
		if (magazineCount < 1) { reload() }
		if (actionPoints < 5) { return }
		var tagReturn : bool = false
		var enemyStanceVal : integer = 2
		var energyZero : bool = false
		if (battleground.GetIntegerValue(blue.GetX, blue.GetY) === 2) { enemyStanceVal = 2 }
		else if (battleground.GetIntegerValue(blue.GetX, blue.GetY) === 3) { enemyStanceVal = 0 }
		else {
			switch (blue.GetStance) {
				case "kneeling" : {	enemyStanceVal = 1 }
				case "lying" : { enemyStanceVal = 0	}
			}
		}
		switch (currStance) {
			case "standing" : {
				if (random(10) + 1 + enemyStanceVal > 8) {
					energyZero = blue.Tagged
					tagReturn = true
				}
			}
			case "kneeling" : {
				if (random(10) + 1 + enemyStanceVal > 6) {
					energyZero = blue.Tagged
					tagReturn = true
				}
			}
			case "lying" : {
				if (random(10) + 1 + enemyStanceVal > 4) {
					energyZero = blue.Tagged
					tagReturn = true
				}
			}
		}
		if (tagReturn) {
			tagged = true
			taggedCounter = 2
			if (blue.GetEnergy >= 0) { points = points + 10 }
			if (energyZero) { points = points + 10 }
		}
		magazineCount--
		actionPoints = actionPoints - 5
	}
	
	// USER METHOD: tags a yellow enemy
	def tag(yellow : Yellow) {
		if (magazineCount < 1) { reload() }
		if (actionPoints < 5) { return }
		var tagReturn : bool = false
		var enemyStanceVal : integer = 2
		var energyZero : bool = false
		if (battleground.GetIntegerValue(yellow.GetX, yellow.GetY) === 2) { enemyStanceVal = 2 }
		else if (battleground.GetIntegerValue(yellow.GetX, yellow.GetY) === 3) { enemyStanceVal = 0 }
		else {
			switch (yellow.GetStance) {
				case "kneeling" : {	enemyStanceVal = 1 }
				case "lying" : { enemyStanceVal = 0	}
			}
		}
		switch (currStance) {
			case "standing" : {
				if (random(10) + 1 + enemyStanceVal > 8) {
					energyZero = yellow.Tagged
					tagReturn = true
				}
			}
			case "kneeling" : {
				if (random(10) + 1 + enemyStanceVal > 6) {
					energyZero = yellow.Tagged
					tagReturn = true
				}
			}
			case "lying" : {
				if (random(10) + 1 + enemyStanceVal > 4) {
					energyZero = yellow.Tagged
					tagReturn = true
				}
			}
		}
		if (tagReturn) {
			tagged = true
			taggedCounter = 2
			if (yellow.GetEnergy >= 0) { points = points + 10 }
			if (energyZero) { points = points + 10 }
		}
		magazineCount--
		actionPoints = actionPoints - 5
	}
	
	// USER METHOD: checks if an enemy is located at (x, y) and, if so, attempts to tag that enemy
	def tag(x : real, y : real) {
		if (magazineCount < 1) { reload() }
		if (actionPoints < 5) { return }
		var potentialRed = explore Red where [r => return (r.GetX === x) and (r.GetY === y) and hasBeeline(xcor, ycor, r.GetX, r.GetY)]
		var potentialBlue = explore Blue where [b => return (b.GetX === x) and (b.GetY === y) and hasBeeline(xcor, ycor, b.GetX, b.GetY)]
		var potentialYellow = explore Yellow where [ye => return (ye.GetX === x) and (ye.GetY === y) and hasBeeline(xcor, ycor, ye.GetX, ye.GetY)]
		if (length(potentialRed) > 0) { tag(potentialRed[0]) }
		else if (length(potentialBlue) > 0) { tag(potentialBlue[0]) }
		else if (length(potentialYellow) > 0) { tag(potentialYellow[0]) }
		else {
			magazineCount--
			actionPoints = actionPoints - 5
		}
	}
	
	// USER METHOD: refills magazineCount
	def reload() {
		if (actionPoints < 3) {	return }
		actionPoints = actionPoints - 3
		magazineCount = 5
	}

// **************************************************************************************** 
// ***************************** POINTS AND PASSIVES BEGIN ********************************
// ****************************************************************************************

	// refills actionPoints and movementPoints
	def refillPoints() {
		if (battleground.GetIntegerValue(xcor, ycor) === 1) {
			var x : real = xSpawn
			var y : real = ySpawn
			pos at #(x, y)
			pathCalculated = false
		}
		actionPoints = 10
		if (movementDelay > 0) { movementDelay-- }
		if (taggedCounter > 0) { taggedCounter-- }
		else { tagged = false }
		if (wasTaggedCounter > 0) { wasTaggedCounter-- }
		else { wasTagged = false }
		enemiesFromTeam = new List<Tuple<real, real>>
		hasMoved = false
	}
	
	// resets attribute values when energy <= 0
	def resetValues() {
		if (battleground.GetIntegerValue(xcor, ycor) === 2) {
			var hill = nearest Hill
			hill.Free
		} else if (battleground.GetIntegerValue(xcor, ycor) === 3) {
			var ditch = nearest Ditch
			ditch.Free
		}
		energy = 100
		currStance = "standing"
		visualRange = 10
		visibilityRange = 10
		movementDelay = 0
		actionPoints = 10
		wasTagged = false
		wasTaggedCounter = 0
		tagged = false
		taggedCounter = 0
		pathCalculated = false
		stage = 0
		var x : real = xSpawn
		var y : real = ySpawn
		pos at #(x, y)
	}
	
	// USER METHOD: gets agent's memberID
	def getMemberID(a : Green) : integer {
		return a.GetMemberID
	}
	
	// agent got tagged
	passive Tagged() : bool {
		var energyZero = false
		energy = energy - 10
		wasTagged = true
		wasTaggedCounter = 2
		if (energy === 0) {
			points = points - 10
			resetValues()
			energyZero = true
		}
		return energyZero
	}
	
	// returns current visibility range
	passive GetVisibility() => return visibilityRange
	
	// USER METHOD: returns current stance
	passive GetStance() => return currStance
	
	// returns current energy value
	passive GetEnergy() => return energy
	
	// USER METHOD: returns current x-coordinate
	passive GetX() => return xcor
	
	// USER METHOD: returns current y-coordinate
	passive GetY() => return ycor
	
	// returns memberID
	passive GetMemberID() => return memberID
	
	// returns barriers list
	passive GetBarriers() => return barriers
	
	// returns hills list
	passive GetHills() => return hills
	
	// returns ditches list
	passive GetDitches() => return ditches
	
}
